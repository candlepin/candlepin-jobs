apply plugin: 'groovy'

sourceSets {
  jobs {
    groovy {
      srcDirs 'src/jobs'
      compileClasspath += main.compileClasspath
    }
    compileClasspath += sourceSets.main.output
    runtimeClasspath += sourceSets.main.output
  }
}

repositories {
  mavenCentral()
  maven { url 'https://repo.jenkins-ci.org/releases/' }
  maven { url 'https://repo.jenkins-ci.org/public/' }
  // jcenter()
}

configurations {
  testPlugins {}
}

// Exclude buggy Xalan dependency this way the JRE default TransformerFactory is used
// The xalan pulled in by htmlunit does not properly deal with spaces folder / job names
configurations.all*.exclude group: 'xalan'

dependencies {
  implementation 'org.codehaus.groovy:groovy-all:2.4.11'
  // implementation "org.jenkins-ci.plugins:job-dsl-core:${jobDslVersion}"
  implementation "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"

  /* for the rest task
  compile('org.codehaus.groovy.modules.http-builder:http-builder:0.7.2') {
    exclude(module: 'groovy')
  }
  */
  implementation  'org.apache.ant:ant:1.9.7'

  testImplementation 'org.spockframework:spock-core:1.0-groovy-2.4'
  testImplementation 'cglib:cglib-nodep:2.2.2' // used by Spock

  // Jenkins test harness dependencies
  testImplementation('org.jenkins-ci.main:jenkins-test-harness:2.33') {
    exclude group: 'org.netbeans.modules', module: 'org-netbeans-insane' // https://github.com/sheehan/job-dsl-gradle-example/issues/90
  }
  testImplementation("org.jenkins-ci.main:jenkins-war:${jenkinsVersion}") {
    exclude group: 'org.jenkins-ci.ui', module: 'bootstrap' // https://github.com/sheehan/job-dsl-gradle-example/issues/87
  }

  // Job DSL plugin including plugin dependencies
  testImplementation "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
  testImplementation "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
  testImplementation 'org.jenkins-ci.plugins:structs:1.20@jar'
  testImplementation 'org.jenkins-ci.plugins:cloudbees-folder:6.9@jar'
  testImplementation 'org.jenkins-ci.plugins:script-security:1.68@jar'

// Run the following script in the Script Console of your Jenkins instance to generate
// the above testPlugins list. (adapted from https://git.io/fjpUs)
//  note that I did not install all of these, I merely run './gradlew --info test' and only install the ones that
//  are used for the tests or throw a traceback.
/*
    Jenkins.instance.pluginManager.plugins
        .findAll { !(it.shortName in ['job-dsl', 'structs']) }
        .collect { "testPlugins '${it.manifest.mainAttributes.getValue("Group-Id")}:${it.shortName}:${it.version}'" }
        .sort()
        .each { println it }
 */

// plugins to install in test instance
  testPlugins 'com.coravy.hudson.plugins.github:github:1.34.3'
  testPlugins 'org.jenkins-ci.plugins:ansible:1.1'
  testPlugins 'org.jenkins-ci.plugins:ansicolor:1.0.1'
  testPlugins 'org.jenkins-ci.plugins:any-buildstep:0.1'
  testPlugins 'org.jenkins-ci.plugins:build-blocker-plugin:1.7.8'
  testPlugins 'org.jenkins-ci.plugins:cloudbees-folder:6.16'
  testPlugins 'org.jenkins-ci.plugins:conditional-buildstep:1.4.1'
  testPlugins 'org.jenkins-ci.plugins:credentials:2.6.1'
  testPlugins 'org.jenkins-ci.plugins:cvs:2.19'
  testPlugins 'org.jenkins-ci.plugins:email-ext:2.87'
  testPlugins 'org.jenkins-ci.plugins:envinject:2.4.0'
  testPlugins 'org.jenkins-ci.plugins:flexible-publish:0.16.1'
  testPlugins 'org.jenkins-ci.plugins:ghprb:1.42.2'
  testPlugins 'org.jenkins-ci.plugins:git:4.10.3'
  testPlugins 'org.jenkins-ci.plugins:github-branch-source:2.11.4'
  testPlugins 'org.jenkins-ci.plugins:gradle:1.38'
  testPlugins 'org.jenkins-ci.plugins:groovy:2.4'
  testPlugins 'org.jenkins-ci.plugins:htmlpublisher:1.29'
  testPlugins 'org.jenkins-ci.plugins:jms-messaging:1.1.27'
  testPlugins 'org.jenkins-ci.plugins:mailer:408.vd726a_1130320'
  testPlugins 'org.jenkins-ci.plugins:matrix-project:758.v7a_ea_491852f3'
  testPlugins 'org.jenkins-ci.plugins:nodelabelparameter:1.10.3'
  testPlugins 'org.jenkins-ci.plugins:parameterized-trigger:2.44'
  testPlugins 'org.jenkins-ci.plugins:progress-bar-column-plugin:1.0'
  testPlugins 'org.jenkins-ci.plugins:ssh-agent:1.24.1'
  testPlugins 'org.jenkins-ci.plugins:testng-plugin:554.va4a552116332'
  testPlugins 'org.jenkins-ci.plugins:timestamper:1.17'
  testPlugins 'org.jenkins-ci.plugins:token-macro:280.v97a_82642793c'
  testPlugins 'org.jenkins-ci.plugins:view-job-filters:2.3'
  testPlugins 'org.jenkins-ci.plugins:ws-cleanup:0.40'
  testPlugins 'org.jenkins-ci.plugins.workflow:workflow-aggregator:2.7'
  testPlugins 'org.jenkins-ci.plugins.workflow:workflow-api:1143.v2d42f1e9dea_5'
  testPlugins 'org.jenkins-ci.plugins.workflow:workflow-cps-global-lib:564.ve62a_4eb_b_e039'
  testPlugins 'org.jenkins-ci.plugins.workflow:workflow-cps:2660.vb_c0412dc4e6d'
  testPlugins 'org.jenkins-ci.plugins.workflow:workflow-job:1145.v7f2433caa07f'
  testPlugins 'org.jenkins-ci.plugins.workflow:workflow-step-api:622.vb_8e7c15b_c95a_'
  testPlugins 'org.jvnet.hudson.plugins:ircbot:2.42'
}

task resolveTestPlugins(type: Copy) {
  from configurations.testPlugins
  into new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
  include '*.hpi'
  include '*.jpi'
  def mapping = [:]

  doFirst {
    configurations.testPlugins.resolvedConfiguration.resolvedArtifacts.each {
      mapping[it.file.name] = "${it.name}.${it.extension}"
    }
  }
  rename { mapping[it] }

  doLast {
    List<String> baseNames = source*.name.collect {
      mapping[it] }.collect {
        it[0..it.lastIndexOf('.') - 1] }
    new File(destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
  }
}

test {
  dependsOn tasks.resolveTestPlugins
  inputs.files sourceSets.jobs.groovy.srcDirs

  // set build directory for Jenkins test harness, JENKINS-26331
  systemProperty 'buildDirectory', project.buildDir.absolutePath
}

/*
task rest(dependsOn: 'classes', type: JavaExec) {
  main = 'com.dslexample.rest.RestApiScriptRunner'
  classpath = sourceSets.main.runtimeClasspath
  systemProperties System.getProperties()
}
*/
